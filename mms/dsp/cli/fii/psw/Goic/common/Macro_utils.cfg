%!/psw/cpp
/*
 * General utility macros for use by process-specific functions
 */

/*
 * Called from the yesNo popup to see which response
 * the operator selected:
 * 0 = No
 * 1 = Yes
 */
symbol	operResponse
symbol	setAction
symbol	nextAction
symbol	cancelAction

macro   checkYesNoResponse      eq? $(operResponse) 1 $(nextAction);\
                                eq? $(operResponse) 0 $(cancelAction);\
                                operResponse 0

/*
 * Perform a refresh 1 second after entering
 * a new screen if the last screen was the hp
 * display - this fixes the problem of the hp eqmt icon
 * lingering on the new window due to the Macro thread and
 * the canvas thread running at different speeds/priorities
 */
macro	CheckScreenUpdate	eq? "$(lastwin)" "hp" WinMgr after:1:'WinMgr refresh'

/*
 * Trap unresponive central computer errors during download
 * If no response then unpost the wUpdate window after 15 
 * seconds - this prevents the whole system from hanging on the
 * download window if Central doesn't respond
 */
symbol	unpostWindow	
macro	SetUnpostAction		unpostWindow $(actwin);\
				WinMgr after:$(ComMgr timeBeforeGivingUpOnRequest):CheckCurrentWindow
/*
 * See if the current active window is one that has been earmarked for
 * unposting after a certain period of time - unpost it if it is
 */				
macro	CheckCurrentWindow	eq? $(actwin) $(unpostWindow) PostLastWindow

symbol	startFrom
symbol	targetObject

/* Set labels, symbols, etc to values contained
 * in the incoming vector. Target objects must be
 * sequentially named (eg w0.button0, w0.button1, etc )
 * $1 = Object base name	(eg w0.button)
 * $2 = Object type (label, symbol)
 * $3 = Numeric start point (eg if want to label button10 through button20)
 * $4 = vector of string values (eg "Test Label", "Change Hole", etc )
 */

#if #arch(pe)
in	SET-VALUES	0xc7	sbb[s]	targetObject $1;\
	startFrom $3;\
	eq? $2 0 setAction SetLabels2;\
	eq? $2 1 setAction SetSymbols;\
	foreach $4;do TrapLeftBracket;\
	eq? "$(targetObject)" "wInfo.col1Lbl" UpdateW0
#else
in	SET-VALUES	0xc7	sbb[s]	targetObject $1;\
	startFrom $3;\
	eq? $2 0 setAction SetLabels;\
	eq? $2 1 setAction SetSymbols;\
	foreach $4;do TrapLeftBracket;\
	eq? "$(targetObject)" "wInfo.col1Lbl" UpdateW0
#endif

/* 
 * UpdateW0 added to update 1 or 2  fields depend of the king equipment in w0 off SET-VALUES
 * wInfo.col0Lbl0 = 1st field from SHIFT INFORMATION screen. Ties in 
 * with Macro_cfg.cfg file and cfgLbl.111 and 112
 */
#if #class(drill)
macro   UpdateW0        w0.info0 label:$(wInfo.col0Lbl0 label);w0.info1 label:$(wInfo.col0Lbl3 label)
#else
#if #class(shovel) || #class(loader)
macro	UpdateW0	w0.info1 label:$(wInfo.col0Lbl0 label)
#else
macro   UpdateW0
#endif
#endif

/*
 * Check the current vector parameter's value and make sure that it isn't
 * the enclosing bracket. This operation is required when the vector has
 * been saved as a string for future use
 */
macro	TrapLeftBracket	ne? "$2" "[" TrapRightBracket

/*
 * See comments RE TrapLeftBracket
 */
macro	TrapRightBracket	ne? "$2" "]" $(setAction)

/*
 * Turn on hidden Label objects and set the text display values to the
 * current vector parameter's value
 */ 
#if #arch(pe)
macro	SetLabels	$(targetObject)$(+ $(offset) $1 $(startFrom)) label:"$2" map:y
symbol 	offset
macro	SetLabels2	offset $1;\
			foreach $2;do SetLabels
#else
macro	SetLabels	$(targetObject)$(+ $1 $(startFrom)) label:"$2" map:y
#endif
/*
 * Set the value of a symbol based on the current vector parameter's value
 */
macro	SetSymbols	$(targetObject)$(+ $1 $(startFrom)) "$2"

/*
 * Put a message up on all Text objects stating that whatever the operator
 * just pressed isn't turned on
 */
macro	NotEnabled	KDISP "$(cfgLbl.18)";NewTextLine;WinMgr beep:300

#if #class(drill)

/* 
 * Write a carriage return to the various Text objects 
 */
macro	NewTextLine	w0.Text write:"\n";wDrilling.v1 write:"\n"

#else

macro	NewTextLine	w0.Text write:"\n"

#endif


macro	NewTextLine

macro	PostLastWindow	WinMgr window:$(lastwin)

macro	UpdateStatus	for stat;\
			req statreq;\
			updtselect statupdate;\
			menutype Status;\
			Status action:menuaction post

/* Overwrite de Message window functionality to send messages */
symbol  messageFromDisp 
macro   SendMessage     MESSAGE $(Kp2.Disp label)
macro   DoMessageInHp   hp.Text map:y;\
			hp.Text write:$(messageFromDisp)\n;\
			WinMgr after:5:'hp.Text map:n'
macro   DoMessageIn     KBEEP 1;\
			messageFromDisp $1;\
			w0.Text write:"$(cfgLbl.9) $(cfgLbl.11) $2:\n";\
			w0.Text write:$1\n;\
			eq? $(actwin) hp DoMessageInHp
